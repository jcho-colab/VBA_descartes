let
    source = TableDTR,
    mainCG = Excel.CurrentWorkbook(){[Name="usMainCG"]}[Content]{0}[Column1],
    filterID = Table.SelectRows(source, each [country_group] = mainCG),
    renameHS = Table.TransformColumnTypes(Table.RenameColumns(filterID,{"hs", "US HS No."}),{"US HS No.", type text}),
    renamePercentage = Table.TransformColumnTypes(Table.RenameColumns(renameHS,{"percentage", "GEN %"}),{"GEN %", Double.Type}),
    fillMultiplier = Table.ReplaceValue(renamePercentage, null, 1, Replacer.ReplaceValue, {"multiplier"}),
    genAmount = Table.AddColumn(fillMultiplier, "GEN $", each [ratePerUOM]/[multiplier]),
    renameRateUOM = Table.TransformColumnTypes(Table.RenameColumns(genAmount,{"rateUOM", "UOM $"}),{"UOM $", type text}),
    selectCols = Table.SelectColumns(renameRateUOM,{"US HS No.", "GEN %", "GEN $", "UOM $"}),
    addRow = Table.InsertRows(selectCols, 0, {
        [#"US HS No." = "--", #"GEN %" = 0, #"GEN $" = 0,#"UOM $" = ""],
        [#"US HS No." = "0000000000", #"GEN %" = 0, #"GEN $" = 0,#"UOM $" = ""]}),
    addIndex = Table.AddIndexColumn(addRow, "Index", 0, 1, Int64.Type),
    joinNOM = Table.NestedJoin(addIndex, {"US HS No."}, TableNOM, {"hs"}, "TableNOM", JoinKind.LeftOuter),
    expandNOM = Table.ExpandTableColumn(joinNOM, "TableNOM", {"alternate_unit_1", "alternate_unit_2"}, {"STAT UOM", "STAT2 UOM"}),
    sortIndex = Table.Sort(expandNOM,{{"Index", Order.Ascending}}),
    removeIndex = Table.RemoveColumns(sortIndex, "Index"),
    addClient = Table.AddColumn(removeIndex, "Cl.", each 10, Int64.Type),
    configYear = Excel.CurrentWorkbook(){[Name="Year"]}[Content]{0}[Column1],
    addYear = Table.AddColumn(addClient, "Year", each configYear, Int64.Type),
    addCA = Table.AddColumn(Table.AddColumn(addYear, "CAN %", each 0, Int64.Type), "CAN $", each 0, Int64.Type),
    addMX = Table.AddColumn(Table.AddColumn(addCA, "MEX %", each 0, Int64.Type), "MEX $", each 0, Int64.Type),
    addUSMX = Table.AddColumn(Table.AddColumn(addMX, "UMX %", each 0, Int64.Type), "UMX $", each 0, Int64.Type),
    addIL = Table.AddColumn(Table.AddColumn(addUSMX, "OTH %", each 0, Int64.Type), "OTH $", each 0, Int64.Type),
    addOther1 = Table.AddColumn(Table.AddColumn(addIL, "OTH1 %", each 0, Int64.Type), "OTH1 $", each 0, Int64.Type),
    addOther2 = Table.AddColumn(Table.AddColumn(addOther1, "OTH2 %", each 0, Int64.Type), "OTH2 $", each 0, Int64.Type),
    addOther3 = Table.AddColumn(Table.AddColumn(addOther2, "OTH3 %", each 0, Int64.Type), "OTH3 $", each 0, Int64.Type),
    addPercentageUOM = Table.AddColumn(addOther3, "UOM %", each "", type text),
    addStatsQty1 = Table.AddColumn(addPercentageUOM, "STAT QTY", each 0, Int64.Type),
    addStatsQty2 = Table.AddColumn(addStatsQty1, "STAT2 QTY", each 0, Int64.Type),
    addRestriction = Table.AddColumn(addStatsQty2, "Restr", each "", type text),
    reorderCols = Table.ReorderColumns(addRestriction,{"Cl.", "Year", "US HS No.", "GEN %", "GEN $", "CAN %", "CAN $", "MEX %", "MEX $", "UMX %", "UMX $", "OTH %", "OTH $", "OTH1 %", "OTH1 $", "OTH2 %", "OTH2 $", "OTH3 %", "OTH3 $", "UOM %", "UOM $", "STAT UOM", "STAT QTY", "STAT2 UOM", "STAT2 QTY", "Restr"})
in
    reorderCols